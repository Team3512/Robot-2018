plugins {
    id "cpp"
    id "edu.wpi.first.GradleRIO" version "2019.0.0-alpha-3"
    id "edu.wpi.first.GradleVsCode" version "0.3.0"
}

def TEAM = 3512

// Define my targets (RoboRIO) and artifacts (deployable files)
// This is added by GradleRIO's backing project EmbeddedTools.
deploy {
    targets {
        target("roborio", edu.wpi.first.gradlerio.frc.RoboRIO) {
            team = getTeamOrDefault(TEAM)
        }
    }
    artifacts {
        artifact('frcCpp', edu.wpi.first.gradlerio.frc.FRCNativeArtifact) {
            targets << "roborio"
            component = 'FRCUserProgram'
            // Debug can be overridden by command line, for use with VSCode
            debug = getDebugOrDefault(false)
        }
    }
}

model {
    components {
        FRCUserProgram(NativeExecutableSpec) {
            targetPlatform 'roborio'

            sources.cpp {
                source {
                    srcDirs = ['src/cpp']
                    include '**/*.cpp', '**/*.cc'
                }
                exportedHeaders {
                    srcDirs = ['src/include', 'src/include/WPILib']
                    include '**/*.hpp', '**/*.h'
                }
            }

            // Define my dependencies. In this case, WPILib (+ friends), CTRE Toolsuite (Talon SRX)
            // and NavX.
            useLibrary(it, "wpilib", "ctre")
        }

        Test(NativeExecutableSpec) {
            targetPlatform 'roborio'

            binaries.all {
                cppCompiler.define 'TEST'
            }

            sources.cpp {
                source {
                    srcDirs = ['src/cpp', 'test/src/main/native/cpp']
                    include '**/*.cpp', '**/*.cc'
                }
                exportedHeaders {
                    srcDirs = ['src/include', 'src/include/WPILib',
                               'test/src/main/native/include',
                               'test/src/main/native/include/googletest']
                    include '**/*.hpp', '**/*.h'
                }
            }

            // Define my dependencies. In this case, WPILib (+ friends), CTRE Toolsuite (Talon SRX)
            // and NavX.
            useLibrary(it, "wpilib", "ctre")
        }
    }
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.9'
}
